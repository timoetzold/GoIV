/**
 * Apply plugins and other Gradle scripts
 */
apply plugin: 'com.android.application'

apply from: '../checkstyle/checkstyle.gradle'

/**
 * Store keystore properties from keystore.properties in root folder if it exists
 */
def keystoreProperties = new Properties()
def keystorePropertiesFile = rootProject.file('keystore.properties')
if (keystorePropertiesFile.exists()) {
    keystoreProperties.load(new FileInputStream(keystorePropertiesFile))
} else {
    keystoreProperties.keyAlias = 'your_key_alias'
    keystoreProperties.keyPassword = 'your_key_password'
    keystoreProperties.storeFile = 'your_keystore.jks'
    keystoreProperties.storePassword = 'your_keystore_password'
    keystoreProperties.keyAliasPlaystore = 'playstore_key_alias'
    keystoreProperties.keyPasswordPlaystore = 'playstore_key_password'
    keystoreProperties.storeFilePlaystore = 'playstore_keystore.jks'
    keystoreProperties.storePasswordPlaystore = 'playstore_keystore_password'
}

/**
 * Android build
 */
android {
    /**
     * Set configuration for signing release builds with keystore properties defined above
     */
    signingConfigs {
        githubConfig {
            keyAlias keystoreProperties['keyAlias']
            keyPassword keystoreProperties['keyPassword']
            storeFile file(keystoreProperties['storeFile'])
            storePassword keystoreProperties['storePassword']
        }
        playstoreConfig { // App Signing upload key
            keyAlias keystoreProperties['keyAliasPlaystore']
            keyPassword keystoreProperties['keyPasswordPlaystore']
            storeFile file(keystoreProperties['storeFilePlaystore'])
            storePassword keystoreProperties['storePasswordPlaystore']
        }
    }

    /**
     * Android SDK and build-tools version
     * Update (both Gradle and local SDK) whenever possible
     */
    compileSdkVersion 33

    /**
     * Build configurations for APK
     * Ensure the following values are updated before generating release builds:
     * versionCode (Increment of 1)
     * versionName (Based on Semantic Versioning 2.0 - http://semver.org/)
     */
    defaultConfig {
        minSdkVersion 24 /*Dont change this unless you know why*/
        targetSdkVersion 33 /*Dont change this unless you know why*/
        versionCode 81
        versionName "5.8.0"
        testInstrumentationRunner 'androidx.test.runner.AndroidJUnitRunner'
        vectorDrawables.useSupportLibrary = true
    }

    /**
     * Enable default ViewBinding which replaces butterknife
     */
    buildFeatures {
        viewBinding true
    }

    /**
     * Product flavors
     * Offline - No Internet permission and disable Crashlytics
     * Online - Internet permission and enable Crashlytics
     */
    flavorDimensions "default"

    productFlavors {
        offline {
            signingConfig signingConfigs.githubConfig
            applicationId "com.swanberg.pogoiv.nointernet"
            buildConfigField "boolean", "INTERNET_AVAILABLE", "false"
            buildConfigField "boolean", "DISTRIBUTION_GITHUB", "true"
            buildConfigField "boolean", "DISTRIBUTION_PLAYSTORE", "false"
            dimension "default"
        }

        online {
            signingConfig signingConfigs.githubConfig
            applicationId "com.swanberg.pogoiv"
            buildConfigField "boolean", "INTERNET_AVAILABLE", "true"
            buildConfigField "boolean", "DISTRIBUTION_GITHUB", "true"
            buildConfigField "boolean", "DISTRIBUTION_PLAYSTORE", "false"
            dimension "default"
        }

        playstore {
            signingConfig signingConfigs.playstoreConfig
            applicationId = "org.opensource.goiv"
            buildConfigField "boolean", "INTERNET_AVAILABLE", "true"
            buildConfigField "boolean", "DISTRIBUTION_GITHUB", "false"
            buildConfigField "boolean", "DISTRIBUTION_PLAYSTORE", "true"
            dimension "default"
        }
    }

    /**
     * Build type
     * debug - Disable Crashlytics
     * release - Minify and proguard files
     */
    buildTypes {
        debug {
            applicationIdSuffix ".debug"
        }
        release {
            minifyEnabled true
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro',
                    'proguard-guava.pro', 'proguard-okhttp3.pro'
            lintOptions {
                disable 'MissingTranslation'
            }
        }
    }

    splits {
        abi {
            enable true
            reset()
            include 'armeabi-v7a', 'arm64-v8a'/*, 'x86', 'x86_64'*/
            universalApk true
        }
    }

    namespace 'com.kamron.pogoiv'

    android.applicationVariants.all { variant ->
        variant.outputs.all { output ->
            if (output.filters[0]) {
                outputFileName = "GoIV-${variant.name}-${variant.versionName}-${output.filters[0].identifier}.apk"
            } else {
                outputFileName = "GoIV-${variant.name}-${variant.versionName}-universal.apk"
            }

        }
    }
}

/**
 * Project dependencies
 * compile - Compile library package into app APK
 * provided - Compile library package without included into app APK
 * apt - Compile library package without included into app APK (uses android-apt plugin for
 * annotation processing)
 */
dependencies {
    /**
     * Builds Dependencies for All Builds.
     */
    //somehow it won't build without this
    //noinspection AnnotationProcessorOnCompilePath
    compileOnly 'org.projectlombok:lombok:1.18.24'
    annotationProcessor 'org.projectlombok:lombok:1.18.24'
    implementation 'androidx.legacy:legacy-support-v4:1.0.0'
    implementation 'androidx.appcompat:appcompat:1.6.0-rc01'
    //explicitly declare lifecycle-viewmodel-ktx to avoid duplicate class errors
    //noinspection GradleDependency
    implementation "androidx.lifecycle:lifecycle-viewmodel-ktx:2.5.0"
    implementation 'androidx.preference:preference:1.2.0'
    implementation 'androidx.recyclerview:recyclerview:1.2.1'
    implementation 'com.google.android.material:material:1.6.1'
    implementation 'androidx.cardview:cardview:1.0.0'
    implementation 'androidx.constraintlayout:constraintlayout:2.1.4'
    implementation 'androidx.annotation:annotation:1.4.0'
    implementation 'com.google.guava:guava:31.1-android'
    implementation 'com.google.code.gson:gson:2.8.6'
    implementation 'com.google.code.findbugs:jsr305:3.0.2'
    implementation 'org.apache.commons:commons-math3:3.6.1'
    implementation 'com.jakewharton.timber:timber:5.0.1'
    implementation 'com.rmtheis:tess-two:7.0.0'
    implementation 'info.hoang8f:android-segmented:1.0.6'
    implementation 'io.apptik.widget:multislider:1.2'
    implementation 'androidx.palette:palette:1.0.0'
    implementation project(':openCVLibrary330')

    /* for devMethods.thirdparty.pokebattler, temporally set for All Builds */
//    implementation 'org.json:json:20160810' //used to generate moveset list
    implementation 'com.squareup.okhttp3:okhttp:3.10.0'//used to generate moveset list

    /**
     * Online Build Dependencies
     */
    onlineImplementation 'com.squareup.okhttp3:okhttp:3.10.0'

    /**
     * Test Build Dependencies
     */
    testImplementation 'junit:junit:4.12'
    testImplementation 'com.squareup.assertj:assertj-android:1.1.1'
    testImplementation 'org.mockito:mockito-core:3.11.2'
    testImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test.ext:junit:1.1.3'
    androidTestImplementation 'androidx.test:runner:1.4.0'
}

/**
 * Enable Java deprecation/unchecked warnings
 */
allprojects {
    gradle.projectsEvaluated {
        tasks.withType(JavaCompile) {
            options.compilerArgs << "-Xlint:unchecked" << "-Xlint:deprecation"
        }
    }
}
